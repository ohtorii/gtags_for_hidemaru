/*タグファイルの作成を補助するマクロ

ダイアログの設定に基づいてgtagsプログラムを呼び出す補助マクロです。


============================================================
起動方法
============================================================
秀丸の「マクロ実行」から gtags_wrapper.mac を起動してください。

    例えば、ソースコードのフォルダが以下構成の時は、
        c:\my_app
        ├─inc
        ├─src
        └─doc

    ダイアログ上の「フォルダ」に c:\my_app を設定してください。
    「ＯＫボタン」を押すと、gtagsが起動して c:\my_app フォルダに
    タグファイルが生成されます。


============================================================
動作環境
============================================================
・秀丸 ver8以降
・でんがくDLL


============================================================
連絡先
============================================================
http://d.hatena.ne.jp/ohtorii/
https://twitter.com/ohtorii
*/

//バージョン番号
$g_version = "version 1.2.0(2018/10/07)";

$dengaku_dll = hidemarudir + "\\DengakuDLL.dll";
//(.ini)のファイル名
$g_ini_filename 	= "gtags_config.ini";
$g_ini_abs_filename = "";

//gtags.exe へのパス。(.ini)ファイルを解析後に設定される。
$g_exe_global 	= "";

//gtagsの対象となるディレクトリ(directory2=デフォルト値)
$g_param_dir		= directory2;

//gtagsの引数
$g_param_arg		= "-v";

//ユーザーがダイアログに入力した文字列
$g_dialog_dir	= "";
$g_dialog_param	= "";



call Main;
endmacro;


Main:
	call read_gtag_ini;
	if(! ##return){
		return false;
	}

	loaddll $dengaku_dll;
	if (!result) {
		message "田楽DLLのロードに失敗しました\n"
		      + "DengakuDLL.dllが秀丸のディレクトリに存在するか確認してください";
		return false;
	}
	call show_dialog;
	##dialog_result=##return;
	freedll;
	if(! ##dialog_result){
		return ##dialog_result;
	}
	call update_gtag_ini;
	call run_gtags;
	return ##return;


show_dialog:
	if (dllfunc("NEWDIALOG", "[GTAGS] タグファイルの作成 " + $g_version, 128) == 0 ||
		dllfunc("NEWCONTROL", "text", "", "フォルダ") == 0 ||
		dllfunc("SETCTRLWIDTH", "", 8) == 0 ||
		dllfunc("NEWCONTROL", "edit", "CTagsDlg@Dir", $g_param_dir) == 0 ||
		dllfunc("SETCTRLWIDTH", "", 80) == 0 ||
		dllfunc("NEWCONTROL", "refdirbutton", "CTagsDlg@Ref", "") == 0 ||
		dllfunc("SETCTRLITEM", "", "フォルダの選択, \"\", 0") == 0 ||
		dllfunc("SETCTRLNOTIFY", "", "10") == 0 ||
		
		dllfunc("SETCTRLWIDTH", "", 8) == 0 ||
		dllfunc("NEWCONTROL", "button", "CTagsDlg@SetCurDir", "←現在のフォルダを設定する") == 0 ||
		dllfunc("SETCTRLNOTIFY", "", "20") == 0 ||
		
		dllfunc("NEWCONTROL", "text", "", "引数") == 0 ||
		dllfunc("SETCTRLWIDTH", "", 8) == 0 ||
		dllfunc("NEWCONTROL", "edit", "CTagsDlg@Param", $g_param_arg) == 0 ||
		dllfunc("NEWCONTROL", "okcancel", "CTagsDlg@OkCancel", "") == 0) {
		message "ダイアログの作成に失敗しました\n";
		return false;
	}

	// ダイアログの表示
	##n = dllfunc("SHOWDIALOG", 0, 0);
	while (1) {
message_loop:
		$$item = "";
		while ($$item == "") {
			 $$item = dllfuncstr("WAITCTRLNOTIFY",10);
		}
		if ($$item == "0") {
			break;
		}
		else if ($$item == "1") {
			break;
		}
		else if ($$item == "10") {	// 参照ボタン
			##n = dllfunc("SETCTRLSTRING", "CTagsDlg@Dir", dllfuncstr("GETCTRLSTRING", "CTagsDlg@Ref"));
		}
		else if ($$item == "20") {	// 現在のフォルダを設定するボタン
			##n = dllfunc("SETCTRLSTRING", "CTagsDlg@Dir", directory2);
		}
	}

	if ($$item == "0") {
		##n = dllfunc("ENDDIALOG");
		return false;
	}

	$g_dialog_dir 	= dllfuncstr("GETCTRLSTRING", "CTagsDlg@Dir");
	if(existfile($g_dialog_dir, 1)&0x10 != 0x10){
		message("フォルダが見つかりません\n"	+
				"【見つからないフォルダ】\n"+
				$g_dialog_dir + "\n" +
				"【対応方法】\n"+
				"存在するディレクトリを入力して下さい\n"
		);
		goto message_loop;
	}
	
	$g_dialog_param = dllfuncstr("GETCTRLSTRING", "CTagsDlg@Param");
	##n = dllfunc("ENDDIALOG");
	return true;

run_gtags:
	// gtagsの実行
	$$run_str = "\""+ $g_exe_global + "\" " + $g_dialog_param;
	runex $$run_str
			, 0 	//sync	 0:async, 1:sync
			, 0, "" //stdin  0:none, 1:auto, 2:<file, 3:(reserved),
					//		 4:current content, 5:selection
			, 7, "" //stdout 0:none, 1:auto, 2:>file 3:>>file, 4:new window,
					//		 5:insert, 6:replace, 7:>output pane, 8:>>output pane
			, 7, "" //stderr 0:none, 1:auto or >>stdout, 2-8:same as stdout's param
			, 2, $g_dialog_dir //folder 0:none, 1:current, 2:specify 3:(reserved), 4:exe's
			, 2 	//show	 0:auto, 1:show, 2:hide, 3-13:ShellExecute()'s SW_*
			, 0 	//draw	 0:draw, 1:no draw when stdout redirected
			, 0 	//encode 0:ansi, 2:utf-16, 6:utf-8
			, 0 	//extended flags
		;
	
	//
	//memo: 「非同期実行かつアウトプット枠へ出力」なのでrunex文を実行した直後にマクロは終了します。
	//
	
	return ;


read_gtag_ini:
	$g_ini_abs_filename = currentmacrodirectory + "\\" + $g_ini_filename;
	if(! existfile($g_ini_abs_filename)){
		message($g_ini_abs_filename + " が見つかりません");
		return false;
	}

	$g_exe_global = getinistr($g_ini_abs_filename, "Path", "gtags");
	if(! existfile($g_exe_global)){
		message($g_ini_filename + " ファイルの設定エラー\n\n" +
				"【詳細】\n"+
				"[Path]->gtags セクションに指定した実行ファイルが見つかりません。\n" +
				"セクションの値=" + $g_exe_global + "\n\n"+
				"【対応方法】\n"+
				"以下設定ファイルの ”[Path]->gtags セクション” に正しいファイルパスを記述して下さい。\n" +
				$g_ini_abs_filename
				
		);
		return false;
	}

	$g_param_dir = getinistr($g_ini_abs_filename, "Dialog", "dir");
	$g_param_arg = getinistr($g_ini_abs_filename, "Dialog", "arg");
	return true;

update_gtag_ini:
	writeinistr $g_ini_abs_filename, "Dialog", "dir", $g_dialog_dir;
	writeinistr $g_ini_abs_filename, "Dialog", "arg", $g_dialog_param;
	return true;